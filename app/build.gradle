apply plugin: 'com.android.application'
apply plugin: 'maven-publish'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "com.test.uploader"
        minSdkVersion 16
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"
    }

    flavorDimensions "bundle", "api"
    productFlavors {
        basic {
            dimension "bundle"
        }
        extended {
            dimension "bundle"
        }
        full {
            dimension "bundle"
        }
        minapi16 {
            dimension "api"
            minSdkVersion 16
        }
        minapi21 {
            dimension "api"
            minSdkVersion 21
        }
        androidTV {
            dimension "api"
            minSdkVersion 21
        }
        fireTV {
            dimension "api"
            minSdkVersion 21
        }
    }
}

dependencies {
    basicImplementation 'com.google.code.gson:gson:2.8.2'

    extendedImplementation 'com.google.code.gson:gson:2.8.2'
    extendedImplementation 'com.google.android.gms:play-services-ads-identifier:16.0.0'
    extendedImplementation 'com.google.ads.interactivemedia.v3:interactivemedia:3.18.1'

    fullImplementation 'com.google.code.gson:gson:2.8.2'
    fullImplementation 'com.google.android.gms:play-services-ads-identifier:16.0.0'
    fullImplementation 'com.google.ads.interactivemedia.v3:interactivemedia:3.18.1'
    fullImplementation 'com.google.android.gms:play-services-cast-framework:16.2.0'
    fullImplementation 'androidx.mediarouter:mediarouter:1.1.0'
}

publishing {
    publications {

        group project.ext.groupId
        version project.ext.sdkVersion

        // Create different publications for every flavor
        android.variantFilter { variant ->
            if (variant.buildType.name == "release") {
                println variant.flavors.name

                // Dynamically creating publications name
                "${variant.name}"(MavenPublication) {
                    def bundleFlavor = variant.flavors.name[0]
                    def apiFlavor = variant.flavors.name[1]
                    artifactId "${bundleFlavor}-${apiFlavor}"
                    artifact("libs/print-release.aar")
//                    artifact("libs/${project.ext.sdkName}-${bundleFlavor}-${project.ext.sdkVersion}-${apiFlavor}-release.aar")
                    println "libs/${project.ext.sdkName}-${bundleFlavor}-${project.ext.sdkVersion}-${apiFlavor}-release.aar"

                    pom.withXml {
                        def configurationName = "${bundleFlavor}Implementation"

                        //Creating additional node for dependencies
                        def dependenciesNode = asNode().appendNode('dependencies')

                        //Iterate over the {configurationName} dependencies, adding a <dependency> node for each
                        configurations[configurationName].allDependencies.each {
                            if (it.group != null && it.name != null) {
                                println "${configurationName} - adding dependency: ${it.group}:${it.name}:${it.version}"

                                def dependencyNode = dependenciesNode.appendNode('dependency')
                                dependencyNode.appendNode('groupId', it.group)
                                dependencyNode.appendNode('artifactId', it.name)
                                dependencyNode.appendNode('version', it.version)

                                //If there are any exclusions in dependency
                                if (it.excludeRules.size() > 0) {
                                    def exclusionsNode = dependencyNode.appendNode('exclusions')
                                    it.excludeRules.each { rule ->
                                        def exclusionNode = exclusionsNode.appendNode('exclusion')
                                        exclusionNode.appendNode('groupId', rule.group)
                                        exclusionNode.appendNode('artifactId', rule.module)
                                    }
                                }
                            }
                        }
                    }

                }
            }
        }
    }
}
